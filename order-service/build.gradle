plugins {
    id 'java'
}

subprojects {
    if (project.name.endsWith('-starter')) {
        apply plugin: 'org.springframework.boot'
    } else {
        apply plugin: 'java-library'
    }

    apply plugin: 'io.spring.dependency-management'

    configureDependencyManagement(it)

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testImplementation libs.springBootTest

        implementation project(':common:common-domain')
    }
}

project(':order-service:order-starter') {
    dependencies {
        implementation project(':order-service:order-adapters')

        implementation libs.springCloudEurekaClient
        implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    bootJar {
        mainClass = 'com.kozubek.orderstarter.OrderStarterApplication'
    }
}

project(':order-service:order-adapters') {
    dependencies {
        implementation libs.springBootWeb
    }
}