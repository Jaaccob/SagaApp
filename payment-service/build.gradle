plugins {
    id 'java'
}

subprojects {
    if (project.name.endsWith('-starter')) {
        apply plugin: 'org.springframework.boot'
    } else {
        apply plugin: 'java-library'
    }

    apply plugin: 'io.spring.dependency-management'

    configureDependencyManagement(it)

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testImplementation libs.springBootTest

        implementation project(':common:common-domain')
    }
}

project(':payment-service:payment-starter') {
    dependencies {
        implementation project(':payment-service:payment-adapters')

        implementation libs.springCloudEurekaClient
        implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    bootJar {
        mainClass = 'com.kozubek.paymentstarter.PaymentStarterApplication'
    }
}

project(':payment-service:payment-adapters') {
    dependencies {
        implementation libs.springBootWeb
    }
}