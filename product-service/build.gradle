plugins {
	id 'java'
}

subprojects {
    if (project.name.endsWith('-starter')) {
        apply plugin: 'org.springframework.boot'
    } else {
        apply plugin: 'java-library'
    }

    apply plugin: 'io.spring.dependency-management'

    configureDependencyManagement(it)

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testImplementation libs.springBootTest

        implementation project(':common:common-domain')
        implementation project(':common:common-ddd-annotations')
        testImplementation libs.junitJupiter
        testImplementation libs.jqwik
    }

    test {
        useJUnitPlatform()
    }
}

project(':product-service:product-starter') {
    dependencies {
        implementation project(':product-service:product-adapters')
        implementation project(':product-service:product-application')
        implementation project(':product-service:product-domain')
        implementation project(':product-service:product-entities')

        implementation libs.springCloudEurekaClient
        implementation libs.springBootDataJpa
        implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    }

    bootJar {
        mainClass = 'com.kozubek.productstarter.ProductStarterApplication'
    }
}

project(':product-service:product-adapters') {
    dependencies {
        implementation project(':common:common-application') //TODO jk - not sure
        implementation project(':product-service:product-application')
        implementation project(':product-service:product-domain')
        implementation project(':product-service:product-entities')
        implementation project(':infrastructure:kafka')

        implementation libs.springBootWeb
        implementation libs.springBootDataJpa
        implementation libs.liquibase
        implementation libs.postgresql
        
        // Test dependencies
        testImplementation libs.testcontainersJupiter
        testImplementation libs.testcontainersPostgresql
        testImplementation libs.testcontainersKafka
        testImplementation libs.springKafkaTest
    }
}

project(':product-service:product-application') {
    dependencies {
        implementation project(':common:common-application')
        implementation project(':product-service:product-domain')

        implementation libs.springBootWeb
        implementation libs.springBootDataJpa
    }
}

project(':product-service:product-entities') {
    dependencies {
        implementation project(':common:common-entity')
        implementation project(':product-service:product-application')
        implementation project(':product-service:product-domain')

        implementation libs.springBootDataJpa
    }
}

project(':product-service:product-domain') {
    dependencies {
        implementation project(':common:common-domain')

        implementation libs.springBootWeb
    }
}